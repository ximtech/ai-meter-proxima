plugins {
    id 'idea'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ai-meter-proxima'
version = '1.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

repositories {
    mavenCentral()
}

jar {
    enabled = false
}

bootJar {
    archivesBaseName = 'ai-meter-proxima'
}

springBoot {
    buildInfo()
}

ext {
    set('springCloudVersion', "2023.0.0")
    testContainersVersion = '1.19.6'
    spockVersion = '2.4-M2-groovy-3.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.springframework.boot:spring-boot-starter-log4j2"

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'   // log4j2 yaml configuration support

    // Apache utils
    implementation "commons-io:commons-io:2.11.0"
    implementation 'org.apache.commons:commons-lang3:3.0'
    implementation 'commons-validator:commons-validator:1.8.0'
    // Local development tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    // Caching
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.3'
    // Template engine
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.2.3'
    // IronMQ
    implementation 'io.iron.ironmq:ironmq:3.0.5'
    
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Testing, Spock, Groovy
    testImplementation "org.codehaus.groovy:groovy-all:3.0.21"
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation "org.testcontainers:spock:$testContainersVersion"
    implementation platform("org.testcontainers:testcontainers-bom:$testContainersVersion")
    testImplementation 'org.testcontainers:postgresql'
}

configurations {
    all*.exclude group:'org.springframework.boot', module:'spring-boot-starter-logging'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add('-parameters')
    options.compilerArgs.add('-Amapstruct.defaultComponentModel=spring')
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/exception/**',
                    '**/controller/**',
                    '**/*MapperImpl.class'
            ])
        }))
    }
}

test {
    useJUnitPlatform()
    systemProperties System.properties
    classpath = sourceSets.test.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification



